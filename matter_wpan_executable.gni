# Copyright (c) 2023 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/cyw30739_sdk.gni")

import("${chip_root}/examples/platform/infineon/cyw30739/cyw30739_example.gni")
import("${chip_root}/src/app/icd/icd.gni")
import("${chip_root}/src/platform/device.gni")
import("${cyw30739_sdk_build_root}/cyw30739_executable.gni")

if (chip_enable_icd_server) {
  assert(!chip_openthread_ftd, "ICD server works only with FTD disabled.")
}

template("matter_wpan_executable") {
  forward_variables_from(invoker, [ "board" ])

  import("${matter_wpan_sdk_build_root}/boards/${board}/args.gni")

  if (board_use_external_flash) {
    btp = btp_ota_disabled
  } else {
    btp = btp_ota_enabled
  }

  cyw30739_executable(target_name) {
    forward_variables_from(invoker, "*")
    deps += [
      ":cyw30739_example-${board}",
      ":matter_wpan_sdk-${board}",
    ]
  }

  cyw30739_example("cyw30739_example-${board}") {
  }

  group("matter_wpan_sdk-${board}") {
    public_configs = [ ":${target_name}-config" ]

    deps = [
      "${cyw30739_sdk_build_root}:platform-${chip}",
      "${matter_wpan_sdk_build_root}:openthread-${board}",
    ]

    public_deps = [ "${matter_wpan_sdk_build_root}:wpan_sdk-${board}" ]
  }

  config("matter_wpan_sdk-${board}-config") {
  }
}

template("matter_wpan_example") {
  config("${target_name}_config") {
    include_dirs = invoker.include_dirs
  }

  source_set(target_name) {
    sources = invoker.sources

    public_configs = [ ":${target_name}_config" ]
  }
}
